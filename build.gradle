plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group "com.mineinabyss"

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'spigotmc-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/groups/public/'
    }
}

publishing {
    repositories {
        maven {
            name "GitHubPackages"
            url "https://maven.pkg.github.com/MineInAbyss/Geary"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"

    implementation "com.google.auto.value:auto-value-annotations:1.7"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"

    annotationProcessor "com.google.auto.value:auto-value:1.7"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

task copyJar(type: Copy) {
    def plugin_path = project.findProperty("plugin_path")
    inputs.property 'plugin_path', plugin_path
    from shadowJar // here it automatically reads jar file produced from shadowJar task
    into plugin_path
}
